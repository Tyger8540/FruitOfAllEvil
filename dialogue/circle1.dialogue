~ C1_intro
# once again, visuals would be nice, but I think at this point, that's a lot extensive work
# realistically, we'll probably just put the character stills / sprites on screen

##### TODO: SHOW THE CIRCLE 1 POSTER INSTEAD OF THE DIALOGUE
##### TODO: INCLUDE THE C1_level_intro DIALOGUE before C1_level_virgil1,
#####       which should take place in the fruit stand screen

#"...ABANDON EVERY HOPE, ALL YOU WHO ENTER."
#"Oh, don't worry about that, destined traveler," Virgil said to Danny Pilgrim.
#"They use that to scare away anyone with common sense," he continued, "it doesn't apply to us."
#Suddenly, a booming voice came bellowing in front of them.
#"Woe to you, perverted souls, who dare disregard this harrowing sign—"
#An old man with white hair came by on a boat, floating on the River of Acheron.
#"What's up, Charon," Virgil said.
#The infernal boatman, Charon, groaned in disgust.
#"Not this lunatic..."
#Charon recomposed himself and grew fierce upon seeing Danny Pilgrim.
#"You, living soul! You do not belong to the world of the dead. Return to whence you came!"
#"Chill Charon," Virgil said, "this traveler here got a date with THE devil."
#"Do not slander the name of Lucifer! None who is living shall pass through this river!"
#Virgil grinned. "Not even if we pay a fee?"
#"You see, this traveler here is on a journey to give Lucifer the greatest drink of all kind..."
#"Don't you and your folks want a little sip?"
#Charon and the tormented souls surrounding him looked at each other, a curiosity growing in their brows...
set circle_num = 1
set location = "level"
#set section = "C1_level_virgil1"
set section = "C1_level_intro"
set cutscene_speaker = "Virgil"
set get_tree().change_scene_to_file("res://scenes/poster_intro.tscn")
=> END

~ C1_level_intro
# the first dialogue that begins at the fruit stand screen
set cutscene_speaker = "Charon"
set SignalManager.cutscene_speaker_changed.emit()
"ALL HOPE ABANDON YE WHO ENTER HERE."
set cutscene_speaker = "Virgil"
set SignalManager.cutscene_speaker_changed.emit()
Virgil: ...Oh Charon, you are as harrowing as ever.
Virgil: Hm? Do not mind my rambling. It's just a good friend of mine trying to be intimidating.
Virgil: He inscribed those words onto those rocks, to scare away anyone with common sense.
Virgil: Thankfully, it does not apply to us.
set cutscene_speaker = "Charon"
set SignalManager.cutscene_speaker_changed.emit()
???: HO! WHO GOES?!
???: WHO DARES CROSS THE GREAT RIVER ACHERON?!
???: I SHALL SMITE THEE — oh no.
set cutscene_speaker = "Virgil"
set SignalManager.cutscene_speaker_changed.emit()
Virgil: What's up, Charon~
set cutscene_speaker = "Charon"
set SignalManager.cutscene_speaker_changed.emit()
Charon: Ugh, enough with your antics...
set cutscene_speaker = "Virgil"
set SignalManager.cutscene_speaker_changed.emit()
Virgil: Antics they are not. As you may have heard, this traveler is destined to have a date with the devil.
Virgil: So let us cross the river, Charon. In exchange, you and your souls shall get a little sip of mankind's fruits. Deal?
set cutscene_speaker = "Charon"
set SignalManager.cutscene_speaker_changed.emit()
Charon: ...I still hate you, Virgil. But I shall round up these tormented souls shortly.
Charon: You, living soul. You better be prepared to serve them.
=> C1_level_virgil1

~ C1_level_virgil1
# additional tutorialization, at the START of the stand gameplay
set AudioManager.stop_music()
set AudioManager.play_sound(self, "res://audio/music/freya_intensity2.mp3", Enums.Audio_Type.MUSIC)
set cutscene_speaker = "Virgil"
set SignalManager.cutscene_speaker_changed.emit()
Virgil: Ah, it seems I have angered my dear friend. I suppose shall bestow upon you some wisdom.
Virgil: Some customers will desire a fruit in liquid format, what you call a "smoothie."
Virgil: Some customers will want a smoothie of two fruits mixed together.
Virgil: Some customers will even want a mixed order of a smoothie, sliced fruit, and a whole fruit.
Virgil: Be prepared to prepare your customer's order as efficiently as possible.
Virgil: I hear the faster you serve, the more they give in return.
Virgil: Until next time, destined traveler.
set location = "level"
set section = "C1_level_virgil2"
set dialogue_ready = false
set cutscene_speaker = "Virgil"
set SignalManager.tutorial_ended.emit()
=> END

~ C1_level_virgil2
# THE FIRST TIME THE UPGRADE / VIRGIL'S RENTAL STAND is introduced, after round complete
Virgil: I have returned, destined traveler!
Virgil: You see, I have grown quite lonely at my stand, waiting for you to serve all these customers...
Virgil: So I have decided to open up a special stand, exclusively for you!
Virgil: Whenever you get the chance to take a break, I'll be here to offer you some exquisite deals.
Virgil: These deals can upgrade your equipment, fruit stand, you name it... Of course, for a small fee.
Virgil: But don't get too attached! These effects only last for this circle.
Virgil: Likewise, disatisfied customers may leave disgruntled and damage your stand.
Virgil: I, of course, shall repair your cart at the end of your shift...
Virgil: Of course, for a small fee.
set section = "C1_level_charon"
set cutscene_speaker = "Charon"
=> END

# THE BARKS BELOW (CIRCLE I) could actually be used for general customer types

~ C1_level_barks_success
# barks when you finish an order in a timely manner (green bar)
% Thank the heavens…
% Thank God I’m in Hell!
% What a divine taste…
% Six stars!
=> END

~ C1_level_barks_late
# barks when you finish an order late (red bar)
% Took you long enough!
% Finally…
% Time felt very slow just now.
% Good luck dealing with Lucifer.
% Humans really are inefficient creatures…
=> END

~ C1_level_barks_damage
# barks when you are unable to finish an order (depleted bar)
% You won’t last in Hell…
% Rotting flesh is better than this.
% Could’ve done it myself.
% Incompetent service.
% Zero stars.
=> END

~ C1_level_charon
# intro text, when charon first appears on screen
Charon: Ho, living soul! I shall ignore your trespassing in exchange for your drink!
Charon: Do not disappoint these tormented souls.
Charon: And do not disappoint me.
set section = "C1_level_charon2"
set boss_queued = true
set cutscene_speaker = "Charon"
set SignalManager.day_started.emit()
=> END
#- Continue => C1_level_charon2

~ C1_level_charon_barks_new
# barks when Charon arrives on boat with a new set of customers
Charon: And here we have a living soul and their fruit stand...
% Charon: Ho, a new boat of customers!
% Charon: You shall serve these tormented souls!
=> END

~ C1_level_charon_barks_complete
# barks when Charon's boat orders have been completely fulfilled.
Charon: Lo, you hear their cries of satisfaction?!
% Charon: Very impressive work, living soul!
% Charon: The demand will only get stronger...
=> END

~ C1_level_charon_barks_arrive
# barks when Charon arrives on boat (incomplete orders)
Charon: Impatience grows wave by wave...
% Charon: Will you satisfy this boat of souls?
% Charon: This seems to be taking a toll on you...
=> END

~ C1_level_charon_barks_depart
# barks when Charon departs on boat (incomplete orders)
Charon: A strong wave incoming, hold onto your souls!
% Charon: Acheron, calm your tides!
% Charon: I shall fetch some more souls soon.
=> END

~ C1_level_charon2
# level complete
Charon: You have earned the right to pass through.
Charon: Come aboard my boat, and I shall bring you across the river.
set cutscene_speaker = "Virgil"
set SignalManager.cutscene_speaker_changed.emit()
Virgil: Oh, how generous of you, Charon.
set cutscene_speaker = "Charon"
set SignalManager.cutscene_speaker_changed.emit()
Charon: My apologies, Virgil, this boat is full. Go find another way to cross the waters.
set cutscene_speaker = "Virgil"
set SignalManager.cutscene_speaker_changed.emit()
Virgil: As harrowing as ever... Destined traveler, I shall meet you in the next circle! Take care...
set cutscene_speaker = "Charon"
set SignalManager.cutscene_speaker_changed.emit()
...
Charon: Ugh, what an abhorent man... You, living soul.
Charon: Let us hope you do not meet the same fate as the previous ones he brought over.
- Continue
	set location = "market"
	set section = "C1_market"
	set level_defeated = false
	set cutscene_speaker = ""
	set get_tree().change_scene_to_file("res://scenes/farmers_market.tscn")
	=> END

~ C1_market
- Virgil's Stand
	set location = "market_virgil_intro"
#	=> END
	=> market_virgil_intro
- Ovid's Stand
	set location = "market_ovid_default"
#	=> END
	=> market_ovid_default
- Onward to Next Circle => market_end

~ market_end
Are you sure you want to leave?
- Yes
	set dialogue_file = "res://dialogue/circle2.dialogue"
	set dialogue_ready = true
	set location = "intro"
	set section = "C2_intro"
	set talked_to_virgil = false
	set circle_num = 2
	set get_tree().change_scene_to_file("res://scenes/fullscreen_cutscene.tscn")
	=> END
- No
	set SignalManager.market_dialogue_ended.emit()
	=> END

~ market_virgil_intro
Virgil: Oh, how low can you go, destined traveler?
Virgil: Welcome to the first circle of Hell, Limbo!
Virgil: Enjoy this nice reprieve in the castle meadows.
Virgil: The rest of Hell won't look this pretty.
# it would be interesting here if you could possibly change dialogue depending on whether
# the player had checked out Ovid's Stand FIRST.
Virgil: Enough talk, why don't you talk to the other vendor here?
Virgil: After you purchase something from me, of course.
# ALT DIALOGUE:
# Virgil: Oh, it seems you've already talked to the other vendor?
# Virgil: I hope you still have enough to purchase my wares as well.

~ market_virgil_default
Virgil: Welcome to my humble stand, destined traveler.
- Shop => market_virgil_shop
- Chat => market_virgil_chat
- Exit => market_virgil_end

~ market_virgil_shop
Virgil: What seems ripe for the picking?
- Buy => market_virgil_shop_thanks
- Back => market_virgil_default

~ market_virgil_shop_thanks
Virgil: Thank you for your patronage.
- Continue => market_virgil_shop

~ market_virgil_chat
- Charon was an interesting fellow.
	Virgil: Indeed, he is. It's a shame he despises me so much.
	Virgil: That's the price I pay for always going in and out of Hell... 
	Virgil: He does not appreciate how much human traffic I bring into this barren landscape.
	Virgil: Anywho, be prepared for future encounters that may shake up your ways of fruit preparation.
	=> market_virgil_chat
- About the other ones you've guided...
	Virgil: Oh? Did Charon mention something about that?
	Virgil: Pay no mind to his hateful slander.
	Virgil: Every human I've brought over is in good shape. I would've gotten some testimonials, but...
	Virgil: All of them are too busy toiling in Hell, in order to repay their debts to me!
	=> market_virgil_chat
- Who do you work for?
	Virgil: I work with many clients.
	Virgil: ...
	Virgil: Not satisfied? Fine.
	Virgil: I currently work for Lucifer — to bring you over, of course.
	Virgil: Angels, devils, humans... I'm not picky, as long as you have what I want.
	=> market_virgil_chat
- Back => market_virgil_default

~ market_virgil_end
Virgil: Until next time, destined traveler.
set SignalManager.market_dialogue_ended.emit()
=> END

~ market_ovid_default
Ovid: Welcome to our castle grounds, traveler! Let me aid you in your journey...
- Shop => market_ovid_shop
- Chat => market_ovid_chat
- Exit => market_ovid_end

~ market_ovid_shop
Ovid: These goods shall transform you...
- Buy => market_ovid_shop_thanks
- Back => market_ovid_default

~ market_ovid_shop_thanks
Ovid: And your payments shall transform me!
- Continue => market_ovid_shop

~ market_ovid_chat
- Why are you here?
	Ovid: Hm... what a broad question. From what I understand, many of us here did not believe in God.
	Ovid: However, none of us have commited any vice under His eyes.
	Ovid: Thus, we have been relegated to this quiet area for eternity...
	Ovid: I guess we should be honored to have such a pleasant resting place.
	=> market_ovid_chat
- What were you doing before you died?
	Ovid: Hm... Forgive me, it has been such a long time.
	Ovid: I would like to believe I had been writing poetry...
	Ovid: Something about tragic lovers? Always finding each other in death, in different forms...
	Ovid: Perhaps my works still exist somewhere on Earth...
	=> market_ovid_chat
- Have you met Virgil before?
	Ovid: Oh, the man who follows you around? Quite a handsome fellow.
	Ovid: Hm... His face seems very familiar to me.
	Ovid: He and I might've known each other, when we were both residing on Earth.
	Ovid: But I suppose that's been a long time ago... We're complete strangers at this point.
	=> market_ovid_chat
- Back => market_ovid_default

~ market_ovid_end
Ovid: Safe travels, I hope you soar through the depths of Hell...
set SignalManager.market_dialogue_ended.emit()
=> END